
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-core:1.10.19")
    testCompile("org.hamcrest:hamcrest-all:1.3")
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/integration-test/java'
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/integration-test/groovy'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

integrationTest.mustRunAfter test

idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        scopes.TEST.plus += [ configurations.integrationTestCompile, configurations.integrationTestRuntime ]
    }
}
